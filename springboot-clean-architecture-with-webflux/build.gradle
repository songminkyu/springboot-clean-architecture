buildscript {
	ext {
		springBootVersion = '2.7.5'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath "io.spring.gradle:dependency-management-plugin:1.0.15.RELEASE"
	}
}

subprojects {
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group = 'kr.chuyong'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = 17

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
		maven { url 'https://repo.spring.io/snapshot' }
	}

	dependencies {
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		implementation 'io.projectreactor:reactor-core'

		//For Debug Purpiose
		// https://mvnrepository.com/artifact/org.assertj/assertj-core
		implementation 'io.projectreactor.tools:blockhound:1.0.7.BUILD-SNAPSHOT'

		testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
		testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
		testImplementation "org.mockito:mockito-core:4.9.0"
		testImplementation "org.mockito:mockito-junit-jupiter:4.9.0"
		testImplementation 'org.assertj:assertj-core:3.23.1'
	}

	bootJar { enabled = false }
	jar { enabled = true }

	test {
		useJUnitPlatform()
	}
}

project(':use-case') {
	dependencies {
		api project(':entity')
	}
}

project(':infrastructure') {
	dependencies {
		api project(':use-case')
		api 'com.github.jasync-sql:jasync-r2dbc-mysql:2.1.7'
		api 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	}
}

project(':presenter') {
	dependencies {
		api project(':use-case')
		api 'org.springframework.boot:spring-boot-starter-webflux'
		api 'org.springframework.boot:spring-boot-starter-validation'
	}
}

project(':config') {
	bootJar { enabled = true }
	jar { enabled = true }

	dependencies {
		api project(':entity')
		api project(':use-case')
		api project(':presenter')
		api project(':infrastructure')

		api 'org.springframework.boot:spring-boot-starter-webflux'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}
}
